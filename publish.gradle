apply plugin: 'maven-publish'

android {
    publishing {
        singleVariant('release') {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = 'com.github.roro2239.stellar'
                artifactId = project.name
                version = '1.0.0'

                pom {
                    name = 'Stellar API'
                    description = 'Stellar privileged API framework based on Shizuku'
                    url = 'https://github.com/roro2239/Stellar-API'

                    licenses {
                        license {
                            name = 'GNU General Public License v3.0'
                            url = 'https://www.gnu.org/licenses/gpl-3.0.html'
                        }
                    }

                    developers {
                        developer {
                            id = 'rorostudio'
                            name = 'RoRo Studio'
                        }
                    }

                    scm {
                        connection = 'scm:git:git://github.com/roro2239/Stellar-API.git'
                        developerConnection = 'scm:git:ssh://github.com/roro2239/Stellar-API.git'
                        url = 'https://github.com/roro2239/Stellar-API'
                    }
                }
            }
        }

        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/roro2239/Stellar-API")
                credentials {
                    username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                    password = project.findProperty("gpr.token") ?: System.getenv("GITHUB_TOKEN")
                }
            }
        }
    }
}
